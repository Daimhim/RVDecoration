apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 118
        versionName "1.1.8"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}
//tasks.withType(JavaCompile) {
//    options.encoding = "UTF-8"
//}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation rootProject.ext.dependencies.libJunit
    androidTestImplementation rootProject.ext.dependencies.libRunner
    androidTestImplementation rootProject.ext.dependencies.libEspressoCore
    debugImplementation rootProject.ext.dependencies.libRecyclerViewV7
}


/** 以下开始是将Android Library上传到jcenter的相关配置**/

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

//项目主页
def siteUrl = 'https://github.com/Daimhim/RVDecoration'    // project homepage
//项目的版本控制地址
def gitUrl = 'https://github.com/Daimhim/RVDecoration.git' // project git
//项目讨论区
def gitissues = 'https://github.com/Daimhim/RVDecoration/issues' // project issues
//包名
group = "org.daimhim.rvdecoration"
//仓库名
def maven_repo = "RVDecoration"
//发布到JCenter上的项目名字，必须填写
def libName = "rvadapter"            //依赖名
// 版本号，下次更新是只需要更改版本号即可
version = android.defaultConfig.versionName
/**  上面配置后上传至jcenter后的编译路径是这样的： compile 'org.daimhim.rvdecoration:rvadapter:1.0.0'  **/

//生成源文件
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
//生成文档
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.encoding "UTF-8"
    options.charSet 'UTF-8'
    options.author true
    options.version true
    options.links "https://github.com/Daimhim/RVDecoration/tree/master/rvadapter/docs/javadoc"
    failOnError false
}

//文档打包成jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
//拷贝javadoc文件
task copyDoc(type: Copy) {
    from "${buildDir}/docs/"
    into "docs"
}

//上传到jcenter所需要的源码文件
artifacts {
    archives javadocJar
    archives sourcesJar
}

// 配置maven库，生成POM.xml文件
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                name "RecyclerView holp class，specification its adapater"
                url siteUrl
                licenses {
                    license {
                        name "The Apache Software License, Version 2.0"
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'Daimhim'
                        name 'Daimhim'
                        email 'Daimhim@163.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

//上传到jcenter
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")    //读取 local.properties 文件里面的 bintray.user
    key = properties.getProperty("bintray.apikey")   //读取 local.properties 文件里面的 bintray.apikey
    configurations = ['archives']
    pkg {
        repo = maven_repo
        name = libName    //发布到JCenter上的项目名字，必须填写
        desc =  "RecyclerView辅助，规范Adapater"   //项目描述
        websiteUrl = siteUrl
        issueTrackerUrl = gitissues
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
