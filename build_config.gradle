project.ext{
    kotlin_version = "1.4.31"
    compile_Sdk_Version = 30
    target_Sdk_Version = 30
    min_Sdk_Version = 19
    build_Tools_Version = "30.0.3"

    //Android 基础模块
    kotlinAndroidDependencies = [
        kotlin_stdlib: "org.jetbrains.kotlin:kotlin-stdlib:1.4.31",
        core_ktx     : 'androidx.core:core-ktx:1.3.2',
    ]
    //Android 单元和UI测试
    androidTest = [
        junit         : 'junit:junit:4.13.2', //testImplementation
        androidx_junit: 'androidx.test.ext:junit:1.1.2', //androidTestImplementation
        espresso_core : 'androidx.test.espresso:espresso-core:3.3.0', //androidTestImplementation
    ]
    //Android原生依赖
    androidxDependencies = [
        appcompat              : 'androidx.appcompat:appcompat:1.2.0',
        recyclerview           : 'androidx.recyclerview:recyclerview:1.2.0',
        paging_runtime_ktx     : 'androidx.paging:paging-runtime-ktx:2.1.2',
        timber                  : 'com.jakewharton.timber:timber:4.7.1',
    ]
    localDependencies = [
        rvadapter              : ':rvadapter',
        rvadapterex             : ':rvadapterex',
        rvdecoration            : ':rvdecoration',
    ]
    applyPlugin = {
        if("app" == this.name){
            project.apply plugin: 'com.android.application'
        }else{
            project.apply plugin: 'com.android.library'
        }
        project.apply plugin: 'kotlin-android'
        project.apply plugin: 'kotlin-kapt'
        project.description "library"
    }
    applyAndroidDefaultConfig = {
          project.android {
               compileSdkVersion compile_Sdk_Version
               buildToolsVersion build_Tools_Version
               defaultConfig {
                    targetSdkVersion target_Sdk_Version
                    minSdkVersion min_Sdk_Version
                    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
                    consumerProguardFiles "consumer-rules.pro"
               }
               compileOptions {
                   sourceCompatibility JavaVersion.VERSION_1_8
                   targetCompatibility JavaVersion.VERSION_1_8
               }
               kotlinOptions {
                   jvmTarget = '1.8'
               }
          }
    }
    applyDependencies = {
        project.dependencies {
            testImplementation androidTest.junit
            androidTestImplementation androidTest.androidx_junit
            androidTestImplementation androidTest.espresso_core
            kotlinAndroidDependencies.each { k, v ->
                compileOnly v
            }
            androidxDependencies.each { k, v ->
                compileOnly v
            }
        }
    }
}
applyPlugin()
applyAndroidDefaultConfig()
applyDependencies()